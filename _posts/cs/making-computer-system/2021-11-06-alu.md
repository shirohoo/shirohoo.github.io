---
layout: post
category:
  - cs
  - making-computer-system
title: ALU 구현
description: |
  16비트 ALU를 구현했다.
image: /assets/img/cs/cs-logo.jpg
accent_image:
  background: url('/assets/img/cs/cs-logo.jpg') center/cover
related_posts:
  - null
published: true
---

* toc
{:toc}

<br />

# ALU(Arithmetic Logic Unit)

---

승재님과 매주 토요일 아침 10시에 만나 7시간씩 진행했으니 3주가 아니고 사실상 3일 걸렸다고 보는게 맞는 것 같다.

다시 기초부터 차근차근 진행해서 오니 이제 나름 이해가 되기 시작했다.

이 책에서 구현하는 ALU는 16비트 ALU이며, 6개의 제어비트(Control Bit)를 입력받아 여러 함수를 처리한다.

여기서 최종 출력에 `zr`, `ng`는 아직 정확히 뭔지 잘 모르겠다. 추후에 알게될까?

책에서는 아래와 같이 설명하고 있다.

<br />

```
 The ALU (Arithmetic Logic Unit).
 
 According to 6 input bits denoted zx,nx,zy,ny,f,no.
 computes one of the following functions:
 
 x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y, x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs.
 
 In addition, the ALU computes two 1-bit outputs:
 
 if the ALU output == 0, zr is set to 1 otherwise zr is set to 0.
 if the ALU output < 0, ng is set to 1 otherwise ng is set to 0.
```

<br />

4비트 연산으로 예를 들어 생각해보자.

<br />

## 제어비트

---

- `zx = 1`
- `nx = 1`
- `zy = 1`
- `ny = 1`
- `f = 0`
- `no = 1`

<br />

## 입력

---

- `x = 1100`
- `y = 0011`

<br />

## 제어비트 적용

---

- `x = zx, nx가 모두 1`이기 때문에 `!0`인 `1111`
- `y = zy, ny가 모두 1`이기 때문에 `!0`인 `1111`

<br />

## 연산 및 결과

---

- `f = 0` 이므로 `x&y = 1111`
- `ng = 1` 이므로 `!(x&y)`가 되어 `0000`
- 결과는 `!x`와 같다 (`x=1111`, `!x=0000`)

<br />

이에 대한 진리표는 다음과 같다.

<br />

![image](https://user-images.githubusercontent.com/71188307/140598502-d2753f09-9588-4b6c-b459-2c02bd8b89df.png)

<br />

그리고 의사코드가 있었는데, 그냥 의사코드 보고 그대로 구현만 하면 됐다.

그동안 공부한게 헛되지 않았던 듯 하다.

우선 회로도를 그린다.

<br />

![image](https://user-images.githubusercontent.com/71188307/140598043-5d310d22-c9b2-41b0-913b-5918ca9e2d50.png)

<br />

![image](https://user-images.githubusercontent.com/71188307/140597778-111126a0-c528-4392-836b-584cebb43bfb.jpg)

<br />

회로도대로 HDL을 작성하고 시뮬레이터에 돌려봤다.

<br />

```text
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
        // if (zx == 1) set x = 0        // 16-bit constant
        // if (nx == 1) set x = !x       // bitwise not
        Mux16(a=x, sel=zx, out=xOut1);
        Not16(in=xOut1, out=notX);
        Mux16(a=xOut1, b=notX, sel=nx, out=xOut2);

        // if (zy == 1) set y = 0        // 16-bit constant
        // if (ny == 1) set y = !y       // bitwise not
        Mux16(a=y, sel=zy, out=yOut1);
        Not16(in=yOut1, out=notY);
        Mux16(a=yOut1, b=notY, sel=ny, out=yOut2);

        // if (f == 1)  set out = x + y  // integer 2's complement addition
        // if (f == 0)  set out = x & y  // bitwise and
        Add16(a=xOut2, b=yOut2, out=xyAdd);
        And16(a=xOut2, b=yOut2, out=xyAnd);
        Mux16(a=xyAnd, b=xyAdd, sel=f, out=result);

        // if (no == 1) set out = !out   // bitwise not
        Not16(in=result, out=notResult);
        Mux16(a=result, b=notResult, sel=no, out=out, out[15]=msb, out[0..7]=leftByte, out[8..15]=rightByte);

        // if (out == 0) set zr = 1
        // 16비를 1비로 압축함과 동시에 16비트 출력 중 단 하나라도 1인 경우 zr = 0 을 출력하게끔
        Or8Way(in=leftByte, out=zrLeft);
        Or8Way(in=rightByte, out=zrRight);
        Or(a=zrLeft, b=zrRight, out=zrOut);
        Not(in=zrOut, out=zr);

        // if (out < 0) set ng = 1
        // 부호비트(MSB)가 1이면 음수이므로 ng = 1이 출력되어야 한다.
        And(a=msb, b=true, out=ng);
}
```

<br />

![Nov-06-2021 13-59-54](https://user-images.githubusercontent.com/71188307/140598589-8b820504-2e65-4c80-bd52-a3e3e182e447.gif)

<br />

이거 하나 하려고 무지막지하게 시간을 때려박았더니 약간 현타가 오긴 하는데... ALU가 모여서 CPU가 되는 거라 하니 수긍이 간다.

<br />
