---
layout: post
category:
    - spring
    - spring-data-jpa
date: 2021-05-31 13:55
title: ì—”í‹°í‹° í´ë˜ìŠ¤ ìë™ ìƒì„±í•˜ê¸°
description: >
    `JPA Entity` ì›í„°ì¹˜ë¡œ ìë™ ìƒì„±í•˜ê¸°
image: /assets/img/spring/spring-data-jpa/hibernate.jpg
related_posts:
    - 
---

* toc
{:toc}
  
&nbsp;  

JPAë¥¼ ì‚¬ìš©í•˜ë‹¤ ë³´ë©´ ë°ì´í„°ë² ì´ìŠ¤ì˜ í…Œì´ë¸” ëª…ì„¸ë¥¼ ë³´ë©´ì„œ ì—”í‹°í‹° í´ë˜ìŠ¤ë¥¼ ì‘ì„±í•˜ëŠ” ì¼ì´ ìì£¼ ìƒê¸´ë‹¤.

ì‹¬ì§€ì–´ í•„ë“œê°€ ìˆ˜ì‹­ ê°œ ì •ë„ ë˜ë©´ ì—”í‹°í‹° í´ë˜ìŠ¤ë¥¼ ë§Œë“œëŠ” ì¼ ìì²´ê°€ ë¬´ì§€ë§‰ì§€í•œ ë…¸ê°€ë‹¤ê°€ ë¼ë²„ë¦¬ê¸° ì‹­ìƒì´ë‹¤.

intelliJëŠ” ì´ ê³¼ì •ì„ ì§€ì›í•´ì¤€ë‹¤.

í•„ìš”í•œ ê²ƒì€ ì˜¤ë¡œì§€ groovy scriptë¿ì´ë‹¤.

&nbsp;  

# 1. IntelliJ database tool ì—°ë™

---

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2FAqJNy%2Fbtq6fPZ8qSt%2FmHDBkiB7mpb8LPSVeibqy0%2Fimg.png)

&nbsp;  

ìì‹ ì´ ì‚¬ìš©í•˜ê³  ìˆëŠ” ë°ì´í„°ë² ì´ìŠ¤ë¥¼ ì—°ë™í•´ì¤€ë‹¤.

&nbsp;  

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2FbZ4jSd%2Fbtq6gLW9erj%2FxK7ONFdFs31AgKirsAQtt1%2Fimg.png)

&nbsp;  

ì ‘ì† ì •ë³´ë¥¼ ëª¨ë‘ ì•Œë§ê²Œ ì…ë ¥í•œ ë‹¤ìŒ `Test Connection`ì„ ëˆŒëŸ¬ í†µê³¼í•˜ë©´ OKë¥¼ ëˆŒëŸ¬ì¤€ë‹¤.

&nbsp;  

# 2. Groovy script ì ìš©

---

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2FbBudZh%2Fbtq58Zipvjz%2Fskbi36EqtGhKy4J0BRhmjk%2Fimg.png)

&nbsp;  

í…Œì´ë¸”ì„ ìš°í´ë¦­í•˜ì—¬ ìœ„ì˜ ìˆœì„œëŒ€ë¡œ í´ë¦­í•´ì¤€ë‹¤.

ê·¸ëŸ¬ë©´ `Generate POJOs.groovy` ë¼ëŠ” íŒŒì¼ì´ ì—´ë¦°ë‹¤.

&nbsp;  

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2F7GSKy%2Fbtq6aPUaFWI%2FRmmtDNp6gVvNEVjDkmkUf0%2Fimg.png)

&nbsp;  

ê¸°ë³¸ì ì¸ scriptê°€ ì‘ì„±ë¼ìˆëŠ”ë°, 

í•´ë‹¹ scriptë¥¼ ê·¸ëŒ€ë¡œ ì‚¬ìš©í•˜ë©´ êµ‰ì¥íˆ ì´ìƒí•œ ì—”í‹°í‹° í´ë˜ìŠ¤ê°€ ë§Œë“¤ì–´ì§€ë¯€ë¡œ

ì´ë¥¼ ì…ë§›ì— ë§ê²Œ ì»¤ìŠ¤í„°ë§ˆì´ì§• í•´ì•¼ í•œë‹¤.

í•„ìê°€ ì»¤ìŠ¤í„°ë§ˆì´ì§•í•œ scriptëŠ” `MSSQL`ì— ë§ì¶”ê¸´ í–ˆëŠ”ë° ~~(íšŒì‚¬ê°€ MSSQLì„ ì“´ë‹¤ ğŸ˜¥)~~

ê·¸ë˜ë„ ëŒ€ë¶€ë¶„ì˜ DBì—ì„œë„ ì‚¬ìš©í•  ìˆ˜ ìˆì„ ê²ƒì´ë¼ ì—¬ê²¨ì§„ë‹¤. ~~(ì•„ë‹ ìˆ˜ë„ ìˆë‹¤.)~~

í˜¹ì‹œë¼ë„ ì˜ ë§ì§€ ì•ŠëŠ”ë‹¤ë©´ ì…ë§›ëŒ€ë¡œ íŠœë‹í•´ì„œ ì‚¬ìš©í•˜ë„ë¡ í•˜ì‹œë¼.

ê¸°ì¡´ì˜ scriptë¥¼ ëª¨ë‘ ì œê±°í•˜ê³  ì•„ë˜ì˜ scriptë¥¼ í†µì§¸ë¡œ ë¶™ì—¬ ë„£ê³  ì €ì¥í•œë‹¤(CTRL + S).

&nbsp;  

```groovy
// file: 'Generate POJOs.groovy'
import com.intellij.database.model.DasTable
import com.intellij.database.model.ObjectKind
import com.intellij.database.util.Case
import com.intellij.database.util.DasUtil
import com.intellij.psi.codeStyle.NameUtil

import javax.swing.*

/**
 * @author olivahn* @link https://github.com/olivahn/create-automation-jpa-entity
 * @param pakageName , primaryKey
 *
 * <pre>
 *
 *     this script's default primary key strategy is @GeneratedValue(strategy = GenerationType.IDENTITY)
 *     and specialized in Microsoft SQL Server
 *     and finally implemented Serializable so recommend that create serial version UID
 *
 *     first. enter your project package name. for example:
 *     >  com.intelliJ.psi
 *
 *     second. enter primary key column name of target database table.
 *     this script is convert input to camel case. for example 1:
 *     >  table primary key column name = MEMBER_ID
 *     >  enter primary key = memberId
 *
 *     example 2:
 *     >  table primary key column name = ID
 *     >  enter primary key = id
 *
 * </pre>
 */

columnType = [
        (~/(?i)bigint/)            : "Long",
        (~/(?i)int/)               : "Integer",
        (~/(?i)bit/)               : "Boolean",
        (~/(?i)decimal/)           : "BigDecimal",
        (~/(?i)float|double|real/) : "Double",
        (~/(?i)datetime|timestamp/): "LocalDateTime",
        (~/(?i)time/)              : "LocalTime",
        (~/(?i)date/)              : "LocalDate",
        (~/(?i)nvarchar/)          : "nvarchar",
        (~/(?i)varchar/)           : "varchar",
        (~/(?i)char/)              : "String"
]

def input = {
    JFrame jframe = new JFrame()
    String answer = JOptionPane.showInputDialog(jframe, it)
    jframe.dispose()
    answer
}

packageName = input("Enter your package name")
primaryKey = input("Enter column name of primary key ")

FILES.chooseDirectoryAndSave("Choose directory", "Choose where to store generated files") { dir ->
    SELECTION.filter {
        it instanceof DasTable && it.getKind() == ObjectKind.TABLE
    }.each {
        generate(it, dir)
    }
}

def generate(table, dir) {
    def tableName = table.getName()
    def className = convertFieldName(tableName, true)
    def fields = categorizeFields(table)
    new File(dir, className + ".java").withPrintWriter {
        out -> generate(out, tableName, className, fields)
    }
}

def generate(out, tableName, className, fields) {
    out.println "package $packageName;"
    out.println ""
    out.println "import javax.persistence.*;"
    out.println "import java.io.Serializable;"
    out.println ""
    out.println "@Entity"
    out.println "@ToString @Getter"
    out.println "@NoArgsConstructor(access = AccessLevel.PROTECTED)"
    out.println "@Table(name = \"$tableName\")"
    out.println "public class $className extends BaseEntity {"
    out.println ""
    fields.each() {
        if (it.annos != "") {
            out.println " ${it.annos}"
        }
        if (it.name == primaryKey) {
            out.println " @Id @GeneratedValue(strategy = GenerationType.IDENTITY)"
        }
        if (it.type == 'nvarchar') {
            out.println " @Nationalized"
            out.println " @Column(name = \"${it.colName}\")"
            out.println " private String ${it.name};"
        } else if (it.type == 'varchar') {
            out.println " @Column(name = \"${it.colName}\")"
            out.println " private String ${it.name};"
        } else {
            out.println " @Column(name = \"${it.colName}\")"
            out.println " private ${it.type} ${it.name};"
        }
        out.println ""
    }
    out.println "}"
}

def categorizeFields(table) {
    DasUtil.getColumns(table).reduce([]) { fields, col ->
        def spec = Case.LOWER.apply(col.getDataType().getSpecification())
        def typeStr = columnType.find {
            p, t -> p.matcher(spec).find()
        }.value
        fields += [[
                           colName: col.getName(),
                           name   : convertFieldName(col.getName(), false),
                           type   : typeStr,
                           annos  : ""]]
    }
}

def convertFieldName(str, capitalize) {
    def s = NameUtil.splitNameIntoWords(str)
            .collect {
                Case.LOWER.apply(it).capitalize()
            }
            .join("")
            .replaceAll(/[^\p{javaJavaIdentifierPart}[_]]/, "_")
    capitalize || s.length() == 1 ? s : Case.LOWER.apply(s[0]) + s[1..-1]
}
```

&nbsp;  

# 3. ì—”í‹°í‹° í´ë˜ìŠ¤ ìƒì„±

---

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2FbpCRyp%2Fbtq6gdM6627%2FVWyEOE5m7qfbP5dF17MgWK%2Fimg.png)

&nbsp;  

ì´ì œ ì—”í‹°í‹° í´ë˜ìŠ¤ë¥¼ ìƒì„±í•˜ê³ ì í•˜ëŠ” í…Œì´ë¸”ì„ ìš°í´ë¦­í•˜ì—¬ ìœ„ì˜ ìˆœì„œëŒ€ë¡œ í´ë¦­í•´ì¤€ë‹¤.

ê·¸ëŸ¼ ì…ë ¥ì°½ì´ ë‘ ë²ˆ ëœ¨ê³ , ìƒì„±ëœ ì—”í‹°í‹° í´ë˜ìŠ¤ë¥¼ ì–´ë–¤ ìœ„ì¹˜ì— ì €ì¥í•  ê²ƒì¸ì§€ ë¬¼ì„ ê²ƒì´ë‹¤.

ì²˜ìŒì€ ìì‹ ì˜ í”„ë¡œì íŠ¸ íŒ¨í‚¤ì§€ëª…ì„ ì…ë ¥í•´ì£¼ê³ ,

ë‘ ë²ˆì§¸ëŠ” í…Œì´ë¸”ì˜ ê¸°ë³¸í‚¤ ì»¬ëŸ¼ì„ camel caseë¡œ ë³€í™˜í•œ ì´ë¦„ì„ ì…ë ¥í•´ì¤€ë‹¤.

ì¼ë°˜ì ìœ¼ë¡œ ë°ì´í„°ë² ì´ìŠ¤ì˜ ì»¬ëŸ¼ëª…ì€ ëŒ€ë¬¸ìë‚˜ ì†Œë¬¸ìì˜ snake caseë¥¼ ì´ìš©í•˜ëŠ” ê²Œ ê´€ë¡€ì´ë¯€ë¡œ,

ì˜¤ë¡œì§€ ì´ ê²½ìš°ë§Œ ì™„ë²½í•˜ê²Œ ê³ ë ¤í•˜ì—¬ ì‘ì„±í•˜ì˜€ë‹¤.

ë§Œì•½ ë‹¤ë¥¸ ë°©ì‹ìœ¼ë¡œ ì‚¬ìš©í•˜ê³  ìˆë‹¤ë©´ scriptë¥¼ ë³€ê²½í•´ì•¼ í•  ìˆ˜ë„ ìˆë‹¤.

&nbsp;  

```text
// ì˜ˆì œ1
í…Œì´ë¸”ëª…: MEMBER
ê¸°ë³¸í‚¤ ì»¬ëŸ¼ëª…: MEMBERID
ì…ë ¥í•´ì•¼ í•  ê°’: memberId

// ì˜ˆì œ2
í…Œì´ë¸”ëª…: MEMBER
ê¸°ë³¸í‚¤ ì»¬ëŸ¼ëª…: ID
ì…ë ¥í•´ì•¼ í•  ê°’: id
```

&nbsp;  

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2FcWSmIg%2Fbtq6b1zWc6P%2FTG7uyE5bfkrhLrYW3BvIik%2Fimg.png)

&nbsp;  

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2Ft0bLB%2Fbtq6b08QQ51%2Fh4BViHdBNQHteHE7AZMOyK%2Fimg.png)

&nbsp;  

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2FcHoqeD%2Fbtq6hTN2qqR%2F5YOIIH0Gg6U2FDngKfcNkK%2Fimg.png)

&nbsp;  

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2FbmgUYy%2Fbtq57GJOf3Z%2FCXwPWSwIwCzKNPxiCOpcM1%2Fimg.png)

&nbsp;  

ê·¸ëŸ¬ë©´ ì´ì²˜ëŸ¼ ì—”í‹°í‹° í´ë˜ìŠ¤ê°€ ìƒì„±ëœë‹¤.

ì´ íŒŒì¼ì„ ì—´ì–´ë³´ë©´ ì•„ë˜ì™€ ê°™ì€ í˜•ì‹ìœ¼ë¡œ ì‘ì„±ë¼ìˆìŒì„ í™•ì¸í•  ìˆ˜ ìˆì„ ê²ƒì´ë‹¤.

&nbsp;  

```java
@Entity
@Getter
@Builder
@ToString
@AllArgsConstructor
@NoArgsConstructor(access = AccessLevel.PROTECTED)
@Table(name = "requestlog")
public class RequestLog implements Serializable {
 @Id @GeneratedValue(strategy = GenerationType.IDENTITY)
 @Column(name = "id")
 private Long id;

 @Column(name = "moddate")
 private LocalDateTime modDate;

 @Column(name = "regdate")
 private LocalDateTime regDate;

 @Column(name = "clientip")
 private String clientIp;

 @Column(name = "httpmethod")
 private String httpMethod;

 @Column(name = "requesturi")
 private String requestUri;
}
```

&nbsp;  

# 4. serialVersionUID ìƒì„±

---

`Hibernate Docs`ì—ì„œëŠ” ëª¨ë“  ì—”í‹°í‹° í´ë˜ìŠ¤ì— ëŒ€í•´ `Serializable` ì¸í„°í˜ì´ìŠ¤ë¥¼ êµ¬í˜„í•˜ëŠ” ê±¸ ê¶Œì¥í•˜ê³  ìˆë‹¤.

ëŒ€ëµ ì—”í‹°í‹° ë§¤í•‘ ë°©ë²•ì— ë”°ë¼ DBì— íŒŒë¼ë¯¸í„°ë¥¼ ë³´ë‚¼ ë•Œ ì§ë ¬í™”í•˜ì—¬ ë³´ë‚´ì•¼ í•˜ëŠ” ê²½ìš°ê°€ ìˆê¸° ë•Œë¬¸ì´ë€ë‹¤.

ì´ ê¶Œê³ ì‚¬í•­ì„ ì§€í‚¤ì§€ ì•Šê³  ìƒí™©ì´ ë§ì•„ë–¨ì–´ì§ˆ ê²½ìš° ê°„í˜¹ `Composite-id class must implement Serializable error` ê°™ì€ê±¸ ë§Œë‚  ìˆ˜ ìˆê¸´ í•œë°, 

ì†”ì§íˆ ì•„ì£¼ ê°€ë” ë‚˜ì˜¤ëŠ” ìƒí™©ì´ë¼ êµ³ì´ í•´ë‹¹ ì¸í„°í˜ì´ìŠ¤ë¥¼ êµ¬í˜„í•˜ì§€ ì•Šì•„ë„ ëœë‹¤ê³  ìƒê°í•˜ê¸´ í•œë‹¤.

&nbsp;  

ê·¸ë˜ë„ ê³µì‹ë¬¸ì„œ ê¶Œê³ ì‚¬í•­ì´ë‹ˆ ê°€ê¸‰ì  ì§€í‚¤ê¸° ìœ„í•´ goorvy scriptì— ë¼ì›Œë„£ì–´ë’€ë‹¤.

`IntelliJ`ëŠ” ì§ë ¬í™”ì‹œ í•„ìš”í•œ `serialVersionUID`ë¥¼ ëœë¤ìœ¼ë¡œ ìƒì„±í•´ì£¼ëŠ” ê¸°ëŠ¥ì´ ìˆë‹¤.

ë¨¼ì € Shiftë¥¼ ë‘ ë²ˆ ì—°ì† ë¹ ë¥´ê²Œ ì…ë ¥í•œë‹¤.

ê·¸ëŸ¬ë©´ ì•„ë˜ì™€ ê°™ì€ ì°½ì´ ëœ¬ë‹¤.

&nbsp;  

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2FpbPNR%2Fbtq6eHVDJjR%2FrpsNHzFMvoosaAu6miDJJ1%2Fimg.png)

&nbsp;  

```text
Serializable class without 'se
```

&nbsp;  

ìœ„ì˜ ë¬¸ìì—´ì„ ë¶™ì—¬ ë„£ì–´ ê²€ìƒ‰í•˜ë©´ ìœ„ì˜ ê¸°ëŠ¥ì´ ê²€ìƒ‰ë˜ëŠ”ë°

ì € ê¸°ëŠ¥ì„ ONìœ¼ë¡œ ë³€ê²½í•´ì£¼ë©´ ëœë‹¤.

ê·¸ë¦¬ê³  ì—”í‹°í‹° í´ë˜ìŠ¤ ì´ë¦„ì— ë§ˆìš°ìŠ¤ë¥¼ ê°–ë‹¤ ëŒ€ë©´...

&nbsp;  

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2Fx2SOE%2Fbtq6cTPwzmw%2FBWknmMb3JpVEMkpKl1v3A1%2Fimg.png)

&nbsp;  

![](https://img1.daumcdn.net/thumb/R1280x0/?scode=mtistory2&fname=https%3A%2F%2Fblog.kakaocdn.net%2Fdn%2FbPBn1r%2Fbtq6gLQpviF%2F7LQ88tJFqOP9AkO4wF9pN1%2Fimg.png)

&nbsp;  
